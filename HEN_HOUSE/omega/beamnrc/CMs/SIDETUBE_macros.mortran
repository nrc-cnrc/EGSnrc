%C80
"#############################################################################"
"                                                                             "
"  EGSnrc BEAMnrc component module macros: cylinders perpendicular to beam    "
"  Copyright (C) 2015 National Research Council Canada                        "
"                                                                             "
"  This file is part of EGSnrc.                                               "
"                                                                             "
"  EGSnrc is free software: you can redistribute it and/or modify it under    "
"  the terms of the GNU Affero General Public License as published by the     "
"  Free Software Foundation, either version 3 of the License, or (at your     "
"  option) any later version.                                                 "
"                                                                             "
"  EGSnrc is distributed in the hope that it will be useful, but WITHOUT ANY  "
"  WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS  "
"  FOR A PARTICULAR PURPOSE.  See the GNU Affero General Public License for   "
"  more details.                                                              "
"                                                                             "
"  You should have received a copy of the GNU Affero General Public License   "
"  along with EGSnrc. If not, see <http://www.gnu.org/licenses/>.             "
"                                                                             "
"#############################################################################"
"                                                                             "
"  Author:          Blake Walters, 1995                                       "
"                                                                             "
"  Contributors:    Iwan Kawrakow                                             "
"                   Frederic Tessier                                          "
"                                                                             "
"#############################################################################"
"                                                                             "
"  The contributors named above are only those who could be identified from   "
"  this file's revision history.                                              "
"                                                                             "
"  This code was originally part of the BEAM code system for Monte Carlo      "
"  simulation of radiotherapy treatments units. It was developed at the       "
"  National Research Council of Canada as part of the OMEGA collaborative     "
"  research project with the University of Wisconsin. The system was          "
"  originally described in:                                                   "
"                                                                             "
"  BEAM: A Monte Carlo code to simulate radiotherapy treatment units,         "
"  DWO Rogers, BA Faddegon, GX Ding, C-M Ma, J Wei and TR Mackie,             "
"  Medical Physics 22, 503-524 (1995).                                        "
"                                                                             "
"  BEAM User Manual                                                           "
"  DWO Rogers, C-M Ma, B Walters, GX Ding, D Sheikh-Bagheri and G Zhang,      "
"  NRC Report PIRS-509A (rev D)                                               "
"                                                                             "
"  As well as the authors of this paper and report, Joanne Treurniet of NRC   "
"  made significant contributions to the code system, in particular the GUIs  "
"  and EGS_Windows. Mark Holmes, Brian Geiser and Paul Reckwerdt of Wisconsin "
"  played important roles in the overall OMEGA project within which the BEAM  "
"  code system was developed.                                                 "
"                                                                             "
"  There have been major upgrades in the BEAM code starting in 2000 which     "
"  have been heavily supported by Iwan Kawrakow, most notably: the port to    "
"  EGSnrc, the inclusion of history-by-history statistics and the development "
"  of the directional bremsstrahlung splitting variance reduction technique.  "
"                                                                             "
"#############################################################################"


"-------------------------------------------------------------------------------
"     CON miscellaneous replacement macros
"-------------------------------------------------------------------------------
REPLACE {$MAX_N_$SIDETUBE} WITH {80}
"       ==================="
;
"-------------------------------------------------------------------------------
"     SIDETUBE component module common
"-------------------------------------------------------------------------------
"
"V>COMMON/CM_$SIDETUBE/
"V>================
"V>ICM_$SIDETUBE = index of CM, set as ICM in INPUT_$SIDETUBE and never re-set
"V>IRSTART_$SIDETUBE    = first region number for this CM
"V>IREND_$SIDETUBE      = last region number for this CM
"V>N_$SIDETUBE          = number of coaxial cylinders in CM
"V>TITLE_$SIDETUBE      = title of CM
"V>ZMIN_$SIDETUBE($MAX_N_$SIDETUBE)      = Front of each layer
"V>ZMAX_$SIDETUBE       = back of each layer
"V>R_$SIDETUBE          = radius at front
"V>XMIN_$SIDETUBE       = lower X edge of cylinders
"V>XMAX_$SIDETUBE       = upper X edge of cylinders
"V>ZCYL_$SIDETUBE       = Z of axis of coaxial cylinders
"V>IR_$SIDETUBE         = local region number
;
REPLACE {;COMIN/CM_$SIDETUBE/;} WITH {
   ;COMMON/CM_$SIDETUBE/
      ICM_$SIDETUBE,
      IRSTART_$SIDETUBE,
      IREND_$SIDETUBE,
      N_$SIDETUBE,
      N_GAP_$SIDETUBE,
      IR_$SIDETUBE,
      ZMIN_$SIDETUBE,
      ZMAX_$SIDETUBE,
      R_$SIDETUBE,
      XMIN_$SIDETUBE,
      XMAX_$SIDETUBE,
      ZCYL_$SIDETUBE,
      TITLE_$SIDETUBE;
   INTEGER
      ICM_$SIDETUBE,
      IRSTART_$SIDETUBE,
      IREND_$SIDETUBE,
      N_$SIDETUBE,
      N_GAP_$SIDETUBE,
      IR_$SIDETUBE;
   $REAL
      ZMIN_$SIDETUBE,
      ZMAX_$SIDETUBE,
      R_$SIDETUBE($MAX_N_$SIDETUBE),
      XMIN_$SIDETUBE,
      XMAX_$SIDETUBE,
      ZCYL_$SIDETUBE;
   CHARACTER*1 TITLE_$SIDETUBE(60);
   } "end of replacement defining common for this CM"
;
;
"-------------------------------------------------------------------------------
"     SIDETUBE HOWNEAR replacement macro
" Now just calls subroutine HOWNEAR_$SIDETUBE
"-------------------------------------------------------------------------------
"V>$SIDETUBE_CM_HOWNEAR(#)
"V>===================
"V>
;
REPLACE {$SIDETUBE_CM_HOWNEAR(#);} WITH {
CALL HOWNEAR_$SIDETUBE({P1});
}
;
REPLACE {$SIDETUBE_IN_HIT(#,#);} WITH {;
A_$SIDETUBE = W(NP)**2+V(NP)**2;
B_$SIDETUBE = (Z(NP)-ZCYL_$SIDETUBE)*W(NP)+Y(NP)*V(NP);
B2_$SIDETUBE = B_$SIDETUBE**2;
C_$SIDETUBE = (Z(NP)-ZCYL_$SIDETUBE)**2+Y(NP)**2;
COUT_$SIDETUBE = C_$SIDETUBE - (R_$SIDETUBE({P2}))**2;
IF(COUT_$SIDETUBE > 0.0)[
   IF(IWATCH > 0)[;OUTPUT COUT_$SIDETUBE;(' COUT=',E11.3);]
   COUT_$SIDETUBE = 0.0;
]
IF(B_$SIDETUBE > 0.0)[
   IF((COUT_$SIDETUBE/B2_$SIDETUBE) > (-1.0E-3))[
      {P1}=-0.5*COUT_$SIDETUBE/B_$SIDETUBE;
   ]
   ELSE[
      {P1}=-COUT_$SIDETUBE/(SQRT(B2_$SIDETUBE-
      A_$SIDETUBE*COUT_$SIDETUBE)+B_$SIDETUBE);
   ]
]
ELSEIF(B_$SIDETUBE < 0.0)[
   IF(COUT_$SIDETUBE/B2_$SIDETUBE > (-1.0E-3))[
      {P1} = -2.*B_$SIDETUBE/A_$SIDETUBE*(1.-
      0.25*A_$SIDETUBE*COUT_$SIDETUBE/B2_$SIDETUBE);
   ]
   ELSE[
      {P1}=(SQRT(B2_$SIDETUBE-A_$SIDETUBE*COUT_$SIDETUBE)-
              B_$SIDETUBE)/A_$SIDETUBE;
   ]
]
ELSEIF(A_$SIDETUBE ~= 0.0)[
   {P1} = SQRT(-COUT_$SIDETUBE/A_$SIDETUBE);
]
}
;

REPLACE {$SIDETUBE_OUT_HIT(#,#,#);} WITH
{;
A_$SIDETUBE  = W(NP)**2+V(NP)**2;
B_$SIDETUBE  = (Z(NP)-ZCYL_$SIDETUBE)*W(NP)+Y(NP)*V(NP);
B2_$SIDETUBE = B_$SIDETUBE**2;
C_$SIDETUBE  = (Z(NP)-ZCYL_$SIDETUBE)**2+Y(NP)**2;
IF(B_$SIDETUBE.GE.0.0 | A_$SIDETUBE = 0.0)[{P1}=0;{P2}=1.0E20]
ELSE[
   COUT_$SIDETUBE = C_$SIDETUBE - (R_$SIDETUBE({P3}))**2;
   IF(COUT_$SIDETUBE < 0.0)[
      IF(IWATCH>0)[OUTPUT COUT_$SIDETUBE;(' CIN=',E11.3);]
      COUT_$SIDETUBE=0.0;
   ]
   RAD_$SIDETUBE = B2_$SIDETUBE - A_$SIDETUBE*COUT_$SIDETUBE;
   IF(RAD_$SIDETUBE < 0.0)[{P1} = 0; {P2}=1E20;]
   ELSE[
      {P1}=1;
      IF(COUT_$SIDETUBE/B2_$SIDETUBE < 1.0E-3)[
         {P2}=-0.5*COUT_$SIDETUBE/B_$SIDETUBE;]
      ELSE[
         IF((SQRT(RAD_$SIDETUBE)-B_$SIDETUBE) ~= 0.0)[
            {P2} = COUT_$SIDETUBE/(SQRT(RAD_$SIDETUBE)-B_$SIDETUBE);
         ]
         ELSE[{P2} = 1.0E30;]
      ]
   ]
]
}
;

REPLACE {$SIDETUBE_END_HIT(#,#,#);} WITH
{;
{P1}=0;
{P2} = ({P3}-X(NP))/U(NP);
IF(SQRT((Y(NP)+V(NP)*{P2})**2 +
   ((Z(NP)-ZCYL_$SIDETUBE)+W(NP)*{P2})**2) <=
    R_$SIDETUBE(N_$SIDETUBE))[
  {P1}=1;
]
}
;


"End of SIDETUBE_macros.mortran (Rev 1.2)"
