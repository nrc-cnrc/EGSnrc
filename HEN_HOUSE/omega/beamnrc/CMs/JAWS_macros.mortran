%C80
"#############################################################################"
"                                                                             "
"  EGSnrc BEAMnrc component module macros: set of paired bars                 "
"  Copyright (C) 2015 National Research Council Canada                        "
"                                                                             "
"  This file is part of EGSnrc.                                               "
"                                                                             "
"  EGSnrc is free software: you can redistribute it and/or modify it under    "
"  the terms of the GNU Affero General Public License as published by the     "
"  Free Software Foundation, either version 3 of the License, or (at your     "
"  option) any later version.                                                 "
"                                                                             "
"  EGSnrc is distributed in the hope that it will be useful, but WITHOUT ANY  "
"  WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS  "
"  FOR A PARTICULAR PURPOSE.  See the GNU Affero General Public License for   "
"  more details.                                                              "
"                                                                             "
"  You should have received a copy of the GNU Affero General Public License   "
"  along with EGSnrc. If not, see <http://www.gnu.org/licenses/>.             "
"                                                                             "
"#############################################################################"
"                                                                             "
"  Author:          George Ding, 1992                                         "
"                                                                             "
"  Contributors:    Blake Walters                                             "
"                   Dave Rogers                                               "
"                   Iwan Kawrakow                                             "
"                                                                             "
"#############################################################################"
"                                                                             "
"  The contributors named above are only those who could be identified from   "
"  this file's revision history.                                              "
"                                                                             "
"  This code was originally part of the BEAM code system for Monte Carlo      "
"  simulation of radiotherapy treatments units. It was developed at the       "
"  National Research Council of Canada as part of the OMEGA collaborative     "
"  research project with the University of Wisconsin. The system was          "
"  originally described in:                                                   "
"                                                                             "
"  BEAM: A Monte Carlo code to simulate radiotherapy treatment units,         "
"  DWO Rogers, BA Faddegon, GX Ding, C-M Ma, J Wei and TR Mackie,             "
"  Medical Physics 22, 503-524 (1995).                                        "
"                                                                             "
"  BEAM User Manual                                                           "
"  DWO Rogers, C-M Ma, B Walters, GX Ding, D Sheikh-Bagheri and G Zhang,      "
"  NRC Report PIRS-509A (rev D)                                               "
"                                                                             "
"  As well as the authors of this paper and report, Joanne Treurniet of NRC   "
"  made significant contributions to the code system, in particular the GUIs  "
"  and EGS_Windows. Mark Holmes, Brian Geiser and Paul Reckwerdt of Wisconsin "
"  played important roles in the overall OMEGA project within which the BEAM  "
"  code system was developed.                                                 "
"                                                                             "
"  There have been major upgrades in the BEAM code starting in 2000 which     "
"  have been heavily supported by Iwan Kawrakow, most notably: the port to    "
"  EGSnrc, the inclusion of history-by-history statistics and the development "
"  of the directional bremsstrahlung splitting variance reduction technique.  "
"                                                                             "
"#############################################################################"


"-------------------------------------------------------------------------------
"     JAWS miscellaneous replacement macros
"-------------------------------------------------------------------------------
REPLACE {$MAX_N_$JAWS} WITH {12}
"       ==================="
;
"-------------------------------------------------------------------------------
"     $JAWS (JAWS) component module common                  ""toc:
"-------------------------------------------------------------------------------
"
"V>COMMON/CM_$JAWS/
"V>================
"V>ICM_$JAWS      = CM index, set as ICM in INPUT_$JAWS - never re-set
"V>IRSTART_$JAWS  = first region number for this CM
"V>IREND_$JAWS    = last region number for this CM
"V>N_$JAWS        = number of regions in CM
"V>ISCM_MAX_$JAWS = number of pired bars in CM
"V>ISCM_$JAWS     = index of pired bars in CM
"V>TITLE_$JAWS    = title of CM
"V>XY_CHOICE_$JAWS = chose X or Y oringtation of the bar
"V>ZBACK_$JAWS    = Back of CM
"V>ZFRONT_$JAWS   = Front of CM
"V>ZMIN_$JAWS(I)  = Zmin of a paired bar
"V>ZMAX_$JAWS(I)  = Zmax of a paired bar
"V>XFP_$JAWS(I)   = Front of positive bar in this ISCM (X or Y)
"V>XFN_$JAWS(I)   = Front of negative bar in this ISCM (X or Y)
"V>XBP_$JAWS(I)   = Back of positive bar in this ISCM (X or Y)
"V>XBN_$JAWS(I)   = Back of negative bar in this ISCM (X or Y)
"V>XFPT_$JAWS(I,2)= Front of positive bar in this ISCM (X or Y)
"V>XFNT_$JAWS(I,2)= Front of negative bar in this ISCM (X or Y)
"V>XBPT_$JAWS(I,2)= Back of positive bar in this ISCM (X or Y)
"V>XBNT_$JAWS(I,2)= Back of negative bar in this ISCM (X or Y)
"V>APO_$JAWS(I)   = Slope of positive bar in this ISCM (X or Y)
"V>ANE_$JAWS(I)   = Slope of negative bar in this ISCM (X or Y)
"V>BPO_$JAWS(I)   = Constant  b of positive bar in equation x = a*z + b
"V>BNE_$JAWS(I)   = Constant  b of negative bar in equation x = a*z + b
"V>BPOT_$JAWS(I,2)= Constant  b of positive bar in equation x = a*z + b
"V>BNET_$JAWS(I,2)= Constant  b of negative bar in equation x = a*z + b
"V>IXY_CHOICE_$JAWS(ISCM) = index for x or y bar direction
"V>COSP_$JAWS(I)  = cos of positive bar in this ISCM
"V>COSN_$JAWS(I)  = cos of negative bar in this ISCM
"V>IR_$JAWS       = local region number
"V>ISCM_$JAWS     = local sub-component region number
"V>XY_$JAWS       = X(NP) or Y(NP)
"V>UV_$JAWS       = U(NP) or V(NP)
;
REPLACE {;COMIN/CM_$JAWS/;} WITH {
   ;COMMON/CM_$JAWS/
      IXY_CHOICE_$JAWS,
      APO_$JAWS, ANE_$JAWS,
      XFP_$JAWS,XFPT_$JAWS,   XFN_$JAWS,XFNT_$JAWS,
      XBP_$JAWS,XBPT_$JAWS,   XBN_$JAWS,XBNT_$JAWS,
      BPO_$JAWS,BPOT_$JAWS,   BNE_$JAWS,BNET_$JAWS,
      ZMIN_$JAWS, ZMAX_$JAWS, ZBACK_$JAWS,ZFRONT_$JAWS,
      COSP_$JAWS,COSN_$JAWS,XY_$JAWS,UV_$JAWS,
      ICM_$JAWS,
      IRSTART_$JAWS,  IREND_$JAWS,  N_$JAWS,  ISCM_MAX_$JAWS,
      IR_$JAWS, ISCM_$JAWS,
      TITLE_$JAWS;
   INTEGER
      ICM_$JAWS,IRSTART_$JAWS,IREND_$JAWS,
      N_$JAWS,ISCM_MAX_$JAWS, IR_$JAWS,
      IXY_CHOICE_$JAWS($MAX_N_$JAWS), ISCM_$JAWS;
   $REAL
      ZMIN_$JAWS($MAX_N_$JAWS),  ZMAX_$JAWS($MAX_N_$JAWS),
      ZBACK_$JAWS, ZFRONT_$JAWS,     XFP_$JAWS($MAX_N_$JAWS),
      XFN_$JAWS($MAX_N_$JAWS),   XBP_$JAWS($MAX_N_$JAWS),
      XBN_$JAWS($MAX_N_$JAWS),   XFPT_$JAWS($MAX_N_$JAWS,2),
      XFNT_$JAWS($MAX_N_$JAWS,2),XBPT_$JAWS($MAX_N_$JAWS,2),
      XBNT_$JAWS($MAX_N_$JAWS,2),APO_$JAWS($MAX_N_$JAWS),
      ANE_$JAWS($MAX_N_$JAWS),   BPO_$JAWS($MAX_N_$JAWS),
      BNE_$JAWS($MAX_N_$JAWS),   BPOT_$JAWS($MAX_N_$JAWS,2),
      BNET_$JAWS($MAX_N_$JAWS,2),COSP_$JAWS($MAX_N_$JAWS),
      COSN_$JAWS($MAX_N_$JAWS), XY_$JAWS,UV_$JAWS;
   CHARACTER*1 TITLE_$JAWS(60);

} "end of replacement defining common for this CM"
;
"-------------------------------------------------------------------------------
"     $JAWS (JAWS) HOWNEAR replacement macro                   ""toc:
"-------------------------------------------------------------------------------
"
REPLACE {$JAWS_CM_HOWNEAR(#)} WITH {  "{P1} is DNEAR"
"       ==================="
CALL HOWNEAR_$JAWS({P1});
}
;
REPLACE {$JAWS_TPOSITIVE;} WITH {;
DIST = (APO_$JAWS(ISCM_$JAWS)*Z(NP) +
        BPOT_$JAWS(ISCM_$JAWS,IB_$JAWS)
         - XY_$JAWS)/(UV_$JAWS-W(NP)*APO_$JAWS(ISCM_$JAWS));
IF(DIST < 0.0)[
   I_tpo = I_tpo + 1;
   DIST = 0.0;
   IF(Z(NP) < ZFRONT_$JAWS-0.1 | Z(NP) > ZBACK_$JAWS+0.1 )[
      OUTPUT; (/' ***WARNING in +ve bar of JAWS'/
            ' Particle is well above or below jaw, yet region number '/
            ' indicates the particle is still in the jaw.'/
            ' The particle is being discarded but this is a bug.'/
            ' Please contact the NRC.'/);
      IDISC=1; RETURN;
   ]
   ELSEIF(I_tpo > 20)[
      OUTPUT; (/' ***WARNING in +ve bar of JAWS'/
            ' DIST calculated to jaw opening has been -ve and reset to '/
            ' zero for the past 20 steps.  The particle could be assigned'/
            ' to the wrong region number.  The particle is being discarded'/
            ' but this is a bug.  Please contact the NRC.'/);
      IDISC = 1; I_tpo = 0; RETURN;
   ]
]
ELSE[
   I_tpo = 0;
              "reset counter in case another particle in this history has"
              "this problem"
]
}
;
REPLACE {$JAWS_TNEGATIVE;} WITH {;
DIST = (ANE_$JAWS(ISCM_$JAWS)*Z(NP)+
          BNET_$JAWS(ISCM_$JAWS,IB_$JAWS)
        - XY_$JAWS)/(UV_$JAWS-W(NP)*ANE_$JAWS(ISCM_$JAWS));
IF(DIST < 0.0)[
   I_tne = I_tne + 1;
   DIST = 0.0;
   IF(Z(NP) < ZFRONT_$JAWS-0.1 | Z(NP) > ZBACK_$JAWS+0.1 )[
      OUTPUT; (/' ***WARNING in -ve bar of JAWS'/
            ' Particle is well above or below jaw, yet region number '/
            ' indicates the particle is still in the jaw.'/
            ' The particle is being discarded but this is a bug.'/
            ' Please contact the NRC.'/);
      IDISC=1; RETURN;
   ]
   ELSEIF(I_tne > 20)[
      OUTPUT; (/' ***WARNING in -ve bar of JAWS'/
            ' DIST calculated to jaw opening has been -ve and reset to '/
            ' zero for the past 20 steps.  The particle could be assigned'/
            ' to the wrong region number.  The particle is being discarded'/
            ' but this is a bug.  Please contact the NRC.'/);
      IDISC = 1; I_tne = 0; RETURN;
   ]
]
ELSE[
   I_tne = 0;
              "reset counter in case another particle in this history has"
              "this problem"
]
}
;
REPLACE {$JAWS_SET_XY_UV(#)} WITH {;
XY_$JAWS = X(NP);
UV_$JAWS = U(NP);
IF(IXY_CHOICE_$JAWS({P1}) = 1)[
   XY_$JAWS = Y(NP);
   UV_$JAWS = V(NP);
]
}
"End of JAWS_macros.mortran (Rev 1.5)"
